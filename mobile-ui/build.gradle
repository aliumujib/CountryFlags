apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'shot'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.aliumujib.countryflags"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        multiDexEnabled true
        testInstrumentationRunner "com.aliumujib.countryflags.runner.TestRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    buildTypes {
        debug {
            buildConfigField "String", "API_URL", '\"https://restcountries.eu/rest/v2/\"'
        }
    }

    androidExtensions {
        experimental = true
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        pickFirst 'mockito-extensions/org.mockito.plugins.MockMaker'
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    def appDependencies = rootProject.ext.appDependencies
    def testDependencies = rootProject.ext.testDependencies

    implementation(
            appDependencies.dagger,
            appDependencies.daggerAndroidSupport,
            appDependencies.recyclerView,
            appDependencies.rxJava,
            appDependencies.rxAndroid,
            appDependencies.design,
            appDependencies.cardView,
            appDependencies.constraintLayout,
            appDependencies.jodaTime,
            appDependencies.lifeCycleExtensions,
            appDependencies.timber,
            appDependencies.javaxAnnotation,
            appDependencies.roomDB,
            appDependencies.rxBinding,
            appDependencies.rxBindingSwipeRefreshLayout,
            appDependencies.glide,
            appDependencies.androidsvg,
            appDependencies.rxbindingAppcompat,
            appDependencies.coreKtx,
            project(':domain'),
            project(':presentation'),
            project(':data'),
            project(':cache'),
            project(':remote')
    )


    kapt appDependencies.daggerCompiler
    kapt appDependencies.daggerAndroidCompiler
    kapt appDependencies.glideCompiler


    testImplementation(
            testDependencies.junit,
            testDependencies.leakCanaryNoOp,
            testDependencies.konveyor,
            testDependencies.kotlinRunnerJUnit,
            testDependencies.kotlinJUnit,
            testDependencies.kotlinRunnerJUnit
    )

    // Instrumentation test dependencies
    androidTestImplementation testDependencies.kotlinJUnit
    androidTestImplementation testDependencies.junit
    androidTestImplementation testDependencies.mockitoAndroid
    androidTestImplementation(testDependencies.espressoCore) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(testDependencies.androidRunner) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(testDependencies.androidRules) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(testDependencies.testCore)
    androidTestImplementation(testDependencies.mockitoKotlin)
    androidTestImplementation(testDependencies.konveyor)
    androidTestImplementation(testDependencies.testExtJunit)

    androidTestImplementation(testDependencies.espressoContrib) {
        exclude module: 'appcompat'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }

    kaptTest testDependencies.daggerCompiler
    kaptAndroidTest testDependencies.daggerCompiler

    if (JavaVersion.current().ordinal() >= JavaVersion.VERSION_1_8.ordinal()) {
        // If you're using @AutoValue or any libs that requires javax.annotation (like Dagger)
        compileOnly 'com.github.pengrad:jdk9-deps:1.0'
        compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

        // If you're using Kotlin
        kapt "com.sun.xml.bind:jaxb-core:2.3.0.1"
        kapt "javax.xml.bind:jaxb-api:2.3.1"
        kapt "com.sun.xml.bind:jaxb-impl:2.3.2"

        // If you're using Java
        annotationProcessor "com.sun.xml.bind:jaxb-core:2.3.0.1"
        annotationProcessor "javax.xml.bind:jaxb-api:2.3.1"

        testAnnotationProcessor "com.sun.xml.bind:jaxb-core:2.3.0.1"
        testAnnotationProcessor "javax.xml.bind:jaxb-api:2.3.1"
    }
}

shot {
    appId = 'com.aliumujib.countryflags'
}